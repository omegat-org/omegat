import org.omegat.documentation.*

plugins {
    id 'org.omegat.documentation'
    id 'org.omegat.common-utilities'
    id 'maven-publish'
    id 'signing'
}

def documentRootDir = file('src_docs')
def styleRootDir = file('src_docs/xsl')

docConfig {
    styleDir.set(styleRootDir)
    docRoot.set(documentRootDir)
    outputRoot.set(layout.buildDirectory.dir('docs'))
}

tasks.register('genDocIndex', Copy) {
    def inputTemplate = layout.projectDirectory.file('src_docs/template/index.html')
    def docPropsFiles = fileTree(dir: 'src_docs/manual', include: '**/version*.properties')
    def langInformation = docPropsFiles.collect { props ->
        def docVersion = loadProperties(props).version
        ['code'  : props.parentFile.name, 'nomanual': false, 'version': docVersion,
         'name'  : Locale.forLanguageTag(props.parentFile.name.replace('_', '-')).getDisplayName(),
         'status': docVersion == version ? 'up-to-date' : 'out-of-date']
    }
    description = 'Generate the docs index file'
    inputs.files docPropsFiles, inputTemplate
    from inputTemplate
    into layout.buildDirectory.dir("tmp/manual/")
    expand('languages': langInformation)
    doLast {
        copy {
            from layout.buildDirectory.file("tmp/manual/index.html")
            into layout.buildDirectory.dir("docs/manual")
        }
    }
    filteringCharset = 'UTF-8'
}

tasks.register('manualZips') {
    description = 'Build ZIP manuals to bundle into application.'
    group = 'other'
}

tasks.register('manualHtmls') {
    description = 'Build HTML manuals and zip for all languages.'
    finalizedBy genDocIndex
    group = 'documentation'
}

ext.makeDocumentationTasks = { lang ->
    def docbookInclude = tasks.register("docbookInclude${lang.capitalize()}", XIncludeTask) {
        styleSheetFile.set(styleDir.file('passthrough.xsl'))
        inputFile.set(layout.projectDirectory.file("src_docs/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
        outputs.files layout.buildDirectory.dir("tmp/manual/${lang}/xhtml5/_index.xml")
    }

    def copyCss = tasks.register("copyCss${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to target'
        into layout.buildDirectory.dir("docs/manual/${lang}")
        from new File(documentRootDir, 'style/omegat.css')
    }

    def copyImages = tasks.register("copyImages${lang.capitalize()}", Copy) {
        description 'Copy Images and CSS file to target'
        into layout.buildDirectory.dir("docs/manual/${lang}/images/")
        from new File(documentRootDir, "manual/${lang}/images/")
    }

    def docbookHtml = tasks.register("docbookHtml${lang.capitalize()}", DocbookHtml) {
        description 'Generate chunked HTML documentation'
        language.set(lang)
        inputFile.set(file("${documentRootDir}/manual/${lang}/OmegaTUsersManual_xinclude_full.xml"))
        mainOutputFile.set(layout.buildDirectory.file("tmp/manual/${language.get()}/xhtml5/_index.html"))
        styleSheetFile.set(styleDir.file("xhtml.xsl"))
        inputs.dir(documentRootDir)
        outputs.files(layout.buildDirectory.file("tmp/${language.get()}/xhtml5/_index.html"))
        dependsOn(docbookInclude, copyImages, copyCss)
    }

    def whcToc = tasks.register("whcToc${lang.capitalize()}", StylesheetTask) {
        description 'Generate whc header and index'
        language.set(lang)
        styleSheetFile.set(styleDir.file('whc-toc.xsl'))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/_index.html"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/toc.xml"))
        dependsOn(docbookHtml)
    }

    def whcIndex = tasks.register("whcIndex${lang.capitalize()}", StylesheetTask) {
        description 'Generate whc header and index'
        language.set(lang)
        styleSheetFile.set(styleDir.file('whc-index.xsl'))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/_index.html"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/index.html"))
        dependsOn(docbookHtml, whcToc)
    }

    def whcHeader = tasks.register("whcHeader${lang.capitalize()}", StylesheetTask) {
        description 'Generate whc header and index'
        language.set(lang)
        styleSheetFile.set(styleDir.file('whc-header.xsl'))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/_index.xml"))
        outputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/_header.xhtml"))
        inputs.files(styleDir.file('whc-header.xsl'), layout.buildDirectory.file("manual/${lang}/xhtml5/_index.xml"))
        outputs.file(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/_header.xhtml"))
        dependsOn(copyCss, docbookInclude)
    }

    def buildDocumentTask = tasks.register("manual${lang.capitalize()}", WhcTask) {
        description 'Build whc contents'
        tocFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/toc.xml"))
        inputFile.set(layout.buildDirectory.file("tmp/manual/${lang}/xhtml5/index.html"))
        outputDirectory.set(outputRoot.get().dir("manual/${lang}"))
        parameterList.set([
                'layout', 'simple',
                // 'user-header', "${language.get()}/xhtml5/_header.xhtml",
                '--navigation-background-color', '#FDFDFD',
                '--field-background-color', '#FDFDFD',
                '--panel-background-color', '#FDFDFD',
                'local-jquery', 'yes'
        ])
        contentFiles.set(fileTree(dir: layout.buildDirectory.dir("tmp/manual/${lang}/xhtml5"), include: '*.html', exclude: '_*'))
        dependsOn(whcHeader, whcToc, whcIndex)
        group('documentation')
    }
    assemble.dependsOn buildDocumentTask
    manualHtmls.dependsOn buildDocumentTask

    def zipTask= tasks.register("manualZip${lang.capitalize()}", Zip) {
        from fileTree(dir: layout.buildDirectory.file("docs/manual/${lang}"))
        exclude 'docs/manual/index.html'
        from fileTree(dir: "src_docs/${lang}", include: '**/version*.properties')
        archiveFileName = "${lang}.zip"
        destinationDirectory = file(layout.buildDirectory.dir("docs/manuals/"))
        dependsOn buildDocumentTask
    }
    assemble.dependsOn zipTask
    manualZips.dependsOn zipTask

    def languageName = new Locale(lang).displayName

    // Create a new publication for this language
    def publication = publishing.publications.create("manual${lang.capitalize()}", MavenPublication) {
        groupId = 'org.omegat'
        artifactId = "omegat-manual-${lang}"
        version = '6.1.0'

        // Add the jar task to the publication
        artifact(zipTask)

        pom {
            name = "OmegaT ${languageName} manual"
            description = "OmegaT HTML manual for language ${languageName}"
            url = 'https://omegat.org'
            scm {
                connection = "scm:git:https://github.com/omegat-org/omegat"
                developerConnection = "scm:git:https://github.com/omegat-org/omegat"
                url = "https://github.com/omegat-org/omegat"
            }
            licenses {
                license {
                    name = 'The GNU General Public License, Version 3.0'
                    url = 'https://www.gnu.org/licenses/licenses/gpl-3.0.html'
                }
            }
            developers {
                developer {
                    id = 'omegat'
                    name = 'OmegaT Developers'
                    email = 'info@omegat.org'
                }
            }
        }
    }

    signing {
        sign publication
        if (!findProperty("signing.keyId")) {
            useGpgCmd()
        }
    }
}

tasks.register('firstSteps') {
    description = 'Build first pages for all languages at src/docs/greetings/.'
    group = 'documentation'
}
assemble.dependsOn firstSteps

ext.firstStepsXmls = fileTree(dir: 'src_docs/greeting', include: '**/First_Steps.xml')
firstStepsXmls.each { xml ->
    def lang = xml.parentFile.name
    def firstStepsHtml = tasks.register("firstSteps${lang.capitalize()}", DocbookHtml) {
        description 'Generate chunked HTML documentation'
        language.set(lang)
        inputFile.set(file("${documentRootDir}/greeting/${lang}/First_Steps.xml"))
        mainOutputFile.set(outputRoot.get().file("greetings/${language.get()}/first_steps.html"))
        styleSheetFile.set(styleDir.file("html.xsl"))
        inputs.files fileTree(dir: "src_docs/greeting/${lang}", include: 'First_Steps.xml')
        outputs.files fileTree(dir: layout.buildDirectory.file('docs/greetings/'),
                includes: ["${lang}/first_steps.html", "${lang}/omegat.css"])
        doLast {
            copy {
                from layout.projectDirectory.file('src_docs/style/omegat.css')
                into outputRoot.get().file("greetings/${language.get()}")
            }
        }
    }
    firstSteps.dependsOn firstStepsHtml
}
