plugins {
    id 'java-library'
    id 'eclipse'
    id 'checkstyle'
    id 'jacoco'
    id 'com.diffplug.spotless'
    id 'com.github.spotbugs'
    id 'org.omegat.common-utilities'
}

// Definition of OmegaT versioning
def localPropsFile = file('local.properties')

// Define target Java version to compatible.
def javaVersion = 11

ext {
    if (localPropsFile.file) {
        loadProperties(localPropsFile).each { k, v ->
            if (!findProperty(k)) {
                set(k, v)
            }
        }
    }
    providedCoreLibsDir = file('lib/provided/core')
    providedModuleLibsDir = file('lib/provided/module')
}

repositories {
    mavenCentral()
    mavenLocal()
}

java {
    withSourcesJar()
    withJavadocJar()
    if (isWindows11 && isArm64) {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    } else {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
            vendor = JvmVendorSpec.ADOPTIUM
        }
    }
}

tasks.named("test", Test) {
    useJUnit()
    workingDir project.rootProject.projectDir
    // Test in headless mode with ./gradlew test -Pheadless
    if (project.hasProperty('headless')) {
        systemProperty 'java.awt.headless', 'true'
    }
    systemProperty 'java.util.logging.config.file', "${rootDir}/config/test/logger.properties"
}

javadoc {
    failOnError = false
    options {
        jFlags('-Duser.language=en')
        addStringOption('locale', 'en_US')
        addStringOption('bottom', '<span>Copyright 2000-2023, OmegaT project and contributors</span>')
        addStringOption('encoding', 'UTF-8')
        addBooleanOption("Xdoclint:none", true)
        addBooleanOption('html5', true)
        addBooleanOption('frames', false)
        addBooleanOption('public', true)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
    options.compilerArgs.addAll '-Xlint', '-Werror'
}

spotbugs {
    reportLevel = com.github.spotbugs.snom.Confidence.valueOf("DEFAULT")
    extraArgs = ['-longBugCodes']
    jvmArgs = ['-Duser.language=en']
    // onlyAnalyze <classes/packages>  only analyze given classes and packages;
    // end with .* to indicate classes in a package, .- to indicate a package prefix
    onlyAnalyze = ['org.omegat.-']
    excludeFilter = project.rootProject.layout.projectDirectory.file("config/spotbugs/exclude.xml")
    runOnCheck = false
}

spotbugsMain {
    reports {
        text.required = true
        html.required = true
    }
}

spotbugsTest {
    reports {
        text.required = true
        html.required = true
    }
}

checkstyle {
    toolVersion = libs.versions.checkstyle.get()
}
checkstyleMain.exclude '**/gen/**'

spotless {
    enforceCheck = false
    java {
        targetExclude 'src/gen/**'
        eclipse().configFile file("${rootDir}/config/spotless/eclipse-formatting.xml")
        removeUnusedImports()
    }
}

tasks.withType(Copy).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
tasks.withType(Sync).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
tasks.withType(Zip).configureEach {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
