plugins {
    id 'org.omegat.java-conventions'
}

dependencies {
    compileOnly(project.rootProject)
    if (providedCoreLibsDir.directory) {
        compileOnly fileTree(dir: providedCoreLibsDir,
                includes: ['**/languagetool-core-*.jar', '**/commons-io-*.jar'])
        implementation fileTree(dir: providedCoreLibsDir,
                includes: ['**/language-ca-*.jar'])
        runtimeOnly fileTree(dir: providedModuleLibsDir, include: '**/catalan-pos-dict-*.jar')
    } else {
        compileOnly(libs.commons.io)
        compileOnly(libs.languagetool.core) {
            exclude module: 'guava'
            exclude module: 'language-detector'
            exclude group: 'com.google.android'
            exclude module: 'jackson-databind'
            exclude group: 'org.jetbrains'
        }
        implementation(libs.languagetool.ca) {
            exclude module: 'languagetool-core'
        }
        compileOnly(libs.lucene.analyzers.common)
    }
    testImplementation(libs.junit4)
    testImplementation(libs.assertj)
    testImplementation(testFixtures(project.rootProject))
    testImplementation(project(":spellchecker:hunspell"))
    testRuntimeOnly(libs.commons.io)
}

test {
    dependsOn jar
    dependsOn project(":spellchecker:hunspell").tasks.withType(Jar)
}

jar {
    archiveFileName.set("omegat-language-ca.${archiveExtension.get()}")
    destinationDirectory.set(rootProject.layout.buildDirectory.dir("modules").get())
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    def pluginProp = loadProperties(file('plugin.properties'))
    manifest {
        attributes('License': 'GNU Public License version 3 or later',
                'Implementation-Version': pluginProp.Version,
                'OmegaT-Plugins': pluginProp.Class,
                'Plugin-Author': pluginProp.Author,
                'Plugin-Version': pluginProp.Version,
                'Plugin-Name': pluginProp.Name,
                'Plugin-Category': pluginProp.Category,
                'Plugin-Description': pluginProp.Description
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
