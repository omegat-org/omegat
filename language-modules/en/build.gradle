plugins {
    id 'org.omegat.module-conventions'
}

dependencies {
    compileOnly(project.rootProject)
    if (providedCoreLibsDir.directory) {
        compileOnly fileTree(dir: providedCoreLibsDir,
                includes: ['**/languagetool-core-*.jar', '**/commons-io-*.jar'])
        implementation fileTree(dir: providedCoreLibsDir,
                includes: ['**/language-en-*.jar'])
        runtimeOnly fileTree(dir: providedModuleLibsDir, includes: ['**/opennlp-*.jar'])
    } else {
        compileOnly(libs.commons.io)
        compileOnly(libs.languagetool.core) {
            exclude module: 'guava'
            exclude module: 'language-detector'
            exclude group: 'com.google.android'
            exclude module: 'jackson-databind'
            exclude group: 'org.jetbrains'
            exclude module: 'commons-lang3'
            exclude module: 'commons-text'
            exclude module: 'commons-logging'
            exclude module: 'json'
            exclude module: 'protobuf-java'
        }
        implementation(libs.languagetool.en) {
            exclude module: 'languagetool-core'
        }
    }
    testImplementation(libs.junit4)
    testImplementation(libs.assertj)
    testImplementation(testFixtures(project.rootProject))
    testImplementation(libs.languagetool.core) {
        exclude module: 'json'
        exclude module: 'protobuf-java'
    }
    testRuntimeOnly(libs.json)
    testRuntimeOnly(libs.protobuf)
    testImplementation project(":spellchecker:morfologik")
    errorprone("com.google.errorprone:error_prone_core:2.36.0")
}

makeModuleTask(loadProperties(file('plugin.properties')))

test {
    dependsOn tasks.withType(Jar)
    dependsOn project(":spellchecker:morfologik").tasks.withType(Jar)
}

