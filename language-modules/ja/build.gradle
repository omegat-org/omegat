plugins {
    id 'org.omegat.java-conventions'
}

dependencies {
    compileOnly(project.rootProject)
    if (providedCoreLibsDir.directory) {
        compileOnly fileTree(dir: providedCoreLibsDir,
                includes: ['**/languagetool-core-*.jar', '**/commons-io-*.jar'])
        implementation fileTree(dir: providedModuleLibsDir,
                includes: ['**/language-ja-*.jar', '**/lucene-gosen-*.jar', '**/icu4j-*.jar'])
    } else {
        compileOnly(libs.commons.io)
        compileOnly(libs.languagetool.core) {
            exclude module: 'guava'
            exclude module: 'language-detector'
            exclude group: 'com.google.android'
            exclude module: 'jackson-databind'
            exclude group: 'org.jetbrains'
        }
        implementation(libs.languagetool.ja) {
            // Temporary exclusion; see https://sourceforge.net/p/omegat/bugs/814/
            exclude module: 'lucene-gosen'
            exclude module: 'languagetool-core'
            exclude module: 'icu4j'
        }
        implementation(dependencies.variantOf(libs.lucene.gosen) { classifier("ipadic") })
        compileOnly(libs.icu4j)
    }

    testImplementation(libs.junit4)
    testRuntimeOnly(libs.slf4j.jdk14)
    testImplementation(project.rootProject)

    testCompileOnly(dependencies.variantOf(libs.lucene.gosen) { classifier("ipadic") })
    testImplementation(libs.languagetool.core) {
        exclude module: 'language-all'
    }
    testRuntimeOnly(libs.languagetool.ja) {
        // Temporary exclusion; see https://sourceforge.net/p/omegat/bugs/814/
        exclude module: 'lucene-gosen'
        exclude module: 'languagetool-core'
        exclude module: 'icu4j'
    }
    testRuntimeOnly(dependencies.variantOf(libs.lucene.gosen) { classifier("ipadic") })
    testRuntimeOnly(libs.icu4j)

    testImplementation(libs.assertj)
    testImplementation(testFixtures(project.rootProject))
    testImplementation(libs.commons.io)
}

test {
    useJUnit()
}

jar {
    archiveFileName.set("omegat-language-ja.${archiveExtension.get()}")
    destinationDirectory.set(rootProject.layout.buildDirectory.dir("modules").get())
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    def pluginProp = loadProperties(file('plugin.properties'))
    manifest {
        attributes('License': 'GNU Public License version 3 or later',
                'Implementation-Version': pluginProp.Version,
                'OmegaT-Plugins': pluginProp.Class,
                'Plugin-Author': pluginProp.Author,
                'Plugin-Version': pluginProp.Version,
                'Plugin-Name': pluginProp.Name,
                'Plugin-Category': pluginProp.Category,
                'Plugin-Description': pluginProp.Description
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
