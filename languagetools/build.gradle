plugins {
    id 'java'
}
repositories {
    mavenCentral()
}

def  languageToolVersion = '6.1'
def plugins = [
        [ lang: 'Arabic', code: 'ar', className: 'arPlugin'],
        [ lang: 'Asturian', code: 'ast', className: 'astPlugin'],
        [ lang: 'Belarusian', code: 'be', className: 'bePlugin'],
        [ lang: 'Breton', code: 'br', className: 'brPlugin'],
        [ lang: 'Catalan', code: 'ca', className: 'caPlugin'],
        [ lang: 'Danish', code: 'da', className: 'daPlugin'],
        [ lang: 'German', code: 'de', className: 'dePlugin'],
        [ lang: 'Greek', code: 'el', className: 'elPlugin'],
        [ lang: 'English', code: 'en', className: 'enPlugin'],
        [ lang: 'Esperanto', code: 'eo', className: 'eoPlugin'],
        [ lang: 'Spanish', code: 'es', className: 'esPlugin'],
        [ lang: 'Percian', code: 'fa', className: 'faPlugin'],
        [ lang: 'French', code: 'fr', className: 'frPlugin'],
        [ lang: 'Irish', code: 'ga', className: 'gaPlugin'],
        [ lang: 'Galcian', code: 'gl', className: 'glPlugin'],
        [ lang: 'Italian', code: 'it', className: 'itPlugin'],
        [ lang: 'Japanese', code: 'ja', className: 'jaPlugin'],
        [ lang: 'Central Khmer', code: 'km', className: 'kmPlugin'],
        [ lang: 'Dutch', code: 'nl', className: 'nlPlugin'],
        [ lang: 'Polish', code: 'pl', className: 'plPlugin'],
        [ lang: 'Portuguese', code: 'pt', className: 'ptPlugin'],
        [ lang: 'Romanian', code: 'ro', className: 'roPlugin'],
        [ lang: 'Rusian', code: 'ru', className: 'ruPlugin'],
        [ lang: 'Slovak', code: 'sk', className: 'skPlugin'],
        [ lang: 'Slovenian', code: 'sl', className: 'slPlugin'],
        [ lang: 'Swedish', code: 'sv', className: 'svPlugin'],
        [ lang: 'Tamil', code: 'ta', className: 'taPlugin'],
        [ lang: 'Tagalog', code: 'tl', className: 'tlPlugin'],
        [ lang: 'Ukrainian', code: 'uk', className: 'ukPlugin'],
        [ lang: 'Chinese', code: 'zh', className: 'zhPlugin']
]

def pluginPackage = 'org.omegat.languagetool.language'

plugins.forEach { args ->
    def name = args.get('code')
    def pluginClass = "${pluginPackage}." + args.get('className')
    def lang = args.get('lang')
    sourceSets.create(name)
    if (name.equals("ja")) {
        dependencies.add("jaImplementation", "org.languagetool:language-ja:${languageToolVersion}") {
            // Temporary exclusion; see https://sourceforge.net/p/omegat/bugs/814/
            exclude module: 'lucene-gosen'
            exclude module: 'languagetool-core'
        }
        dependencies.add("jaImplementation", 'org.omegat.lucene:lucene-gosen:5.5.1:ipadic')
    } else {
        dependencies.add("${name}Implementation", "org.languagetool:language-${name}:${languageToolVersion}") {
            exclude module: 'languagetool-core'
        }
    }
    configurations.getByName("${name}RuntimeClasspath").exclude(group: 'org.languagetool', module: 'language-core')
    tasks.getByName("${name}Classes") {
        group = 'languagetool'
    }
    tasks.register("${name}Jar", Jar) {
        archiveFileName.set("omegat-language-${name}.${archiveExtension.get()}")
        destinationDirectory.set(new File(rootProject.buildDir, "modules"))
        duplicatesStrategy = DuplicatesStrategy.EXCLUDE
        from configurations.getByName("${name}RuntimeClasspath").collect {
            it.isDirectory() ? it : zipTree(it)
        }
        from sourceSets.getByName("${name}").output
        manifest {
            attributes(
                    'Plugin-Name': "${lang} LanguageTool",
                    'Plugin-License': 'GNU Public License version 3 or later',
                    'OmegaT-Plugins': pluginClass,
                    'Plugin-Author': 'OmegaT team',
                    'Plugin-Link': 'https://omegat.org',
                    'Plugin-Description': "Provide ${lang} Languagetool library",
                    'Plugin-Version': "${languageToolVersion}")
        }
        group = 'languagetool'
    }
    jar.dependsOn "${name}Jar"
}
