#!/usr/bin/kaptain

# This version of the Kaptain OmegaT Launcher: 2013-06-03

start "OmegaT Launcher" -> omticon top bottom ;
  top :tabbed  -> locs mem langs connect appearance about ;
  bottom :horizontal -> button_bar ;

omticon -> @icon("images/OmegaT.png") ;

### Locations tab

locs :framed :vertical "Locations" -> javaloc jarfileloc optloc ;
#mandloc :vertical "Mandatory locations" -> javaloc jarfileloc ;

# Location of the Java runtime environment

javaloc "Java runtime environment" -> java_list |! java_pick ;
java_list "System-wide Java installation" ->  @combow (`ls /usr/lib/jvm/*/jre/bin/java`) ;
java_pick :horizontal "Custom Java installation" -> @infile("java")="/opt/omegat/jre/bin/java" javaloc_help ;
javaloc_help :dialog "Help" -> javaloc_m javaloc_hc ;
javaloc_m :horizontal ->  javaloc_t ;
javaloc_t "The current default assumes that you have used the linux-install.sh \n installation script and are using a local version of the JRE (i.e. \n you have installed OmegaT with JRE).\n If you are using a system-wide version of Java that is in your \n execution path, replace /opt/omegat/jre/bin/java with java.\n If you are using a system-wide version of Java that is NOT in your \n execution path, replace /opt/omegat/jre/bin/java with the full path of your \n java executable (in quotes). " -> @ ;
javaloc_hc :horizontal -> @close="Close";


# Location of the OmegaT.jar file (OmegaT version)
# To make a particular OmegaT.jar file the default, replace =""
# with ="<full path of OmegaT.jar file>"

jarfileloc "Location of OmegaT.jar" -> jarlist | ! jarpick ;
jarlist "System-wide OmegaT installation" -> @combow(`find -L /opt/omegat -name "OmegaT.*jar"`);
jarpick :horizontal "Custom OmegaT installation"-> @infile("OmegaT.jar")="/opt/omegat/OmegaT-default/OmegaT.jar" jarfileloc_help ;
jarfileloc_help :dialog "Help" -> jarfileloc_m jarfileloc_hc ;
jarfileloc_m :horizontal ->  jarfileloc_t ;
jarfileloc_t "Location of the OmegaT program. Use this menu item to \nselect a different (installed) version of OmegaT, for example for testing." -> @ ;
jarfileloc_hc :horizontal -> @close="Close" ;

# Optional file locations

optloc "Optional locations" -> configloc projectloc ;

# Specify non-default configuration files

configloc :horizontal "Directory containing non-default configuration files" -> @directory configloc_help ;
configloc_help :dialog "Help" -> configloc_m configloc_hc ;
configloc_m :horizontal ->  configloc_t ;
configloc_t "Use a different set of configuration files to those in your \nsystem. Useful for example if you wish to use a colleague's \nsegmentation rules rather than your own." -> @ ;
configloc_hc :horizontal -> @close="Close" ;

# Open a specific project at launch

#projectloc :horizontal "Open specified project immediately" -> @directory projectloc_help ;
projectloc :horizontal "Open specified project immediately" -> @combow("", `IFS=$(echo "\n\b") && for i in $(find $HOME/Documents $HOME/Desktop -name "omegat.project" -type f); do echo "\"$(dirname $i)\""; done`) projectloc_help ;
projectloc_help :dialog "Help" -> projectloc_m projectloc_hc ;
projectloc_m :horizontal ->  projectloc_t ;
projectloc_t "Open a specific project immediately after launching OmegaT. \nUsualy superfluous in GUI mode." -> @ ;
projectloc_hc :horizontal -> @close="Close" ;

# fillloc -> @fill ;


### Memory tab

mem :framed "Memory" -> memory ;

memory "Assign memory limit for OmegaT" -> defm | halfg | oneg | twog | memory_help ;
defm "Default" -> "" ;
halfg "512 MB" -> " -Xmx512M" ;
oneg "1 GB" -> " -Xmx1024M" ;
twog "2 GB" -> " -Xmx2048M" ;
memory_help :dialog "Help" -> memory_m memory_hc ;
memory_m :horizontal ->  memory_t ;
memory_t "Determine how much RAM OmegaT is allowed to make use of. \nToo low a value will make large projects slow or impossible to open; too high a value \nwill prevent OmegaT from launching." -> @ ;
memory_hc :horizontal -> @close="Close" ;


### languages tab

# Selection of the user interface language
# Add language codes (that OmegaT has) or delete them.
# The first one on the list becomes the default

langs :framed "Languages" -> UI ;

UI "User interface" -> UIlang country languages_help ;

UIlang "User interface language (nn)" -> @combow("en", "ar", "be", "ca", "cs", "cy", "da", "de", "el", "eo", "es", "eu", "fr", "gl", "hu", "id", "it", "ja", "nl", "pl", "pt", "ru", "sh", "sk", "sl", "sq", "sv", "tr", "uk", "zh") ;

# Selection of the user country

country "User country (NN)" -> @string(2) ;

languages_help :dialog "Help" -> languages_m languages_hc ;
languages_m :horizontal ->  languages_t ;
languages_t "Select the language of OmegaT's user interface. If no language \nis specified or a language is not available, OmegaT defaults first to the operating \nsystem language, then to English. Different country variants for the same language are not \nused at present." -> @ ;
languages_hc :horizontal -> @close="Close" ;

### Connectivity tab

connect :framed "Connectivity" -> proxy gt ms ;

proxy "Proxy settings" -> proxyhost proxyport ;


# Enter proxy host IP address

proxyhost "IP address of your proxy server, if your system uses a proxy" -> @string ;

# Proxy Server Port Number

proxyport "Port number used by your system to access the proxy server" -> @string ;

# Google Translate API key

gt "Google Translate API key" -> @password gt_help ;

gt_help :dialog "Help" -> gt_m gt_hc ;
gt_m :horizontal ->  gt_t ;
gt_t "To use Google Translate within OmegaT, you must order \na password from Google and enter it here." -> @ ;
gt_hc :horizontal -> @close="Close";


# Microsoft Translator API key

ms "Microsoft Translator settings" -> msi msk ms_help ;
msi "Microsoft Translator API Client ID" -> @password="" ;
msk "Microsoft Translator API Secret Key" -> @password="" ;


ms_help :dialog "Help" -> ms_m ms_hc ;
ms_m :horizontal ->  ms_t ;
ms_t "To use Mircrosoft Translator within OmegaT, you must order \na client ID and a secret key from Microsoft Azure Marketplace \n(see https://datamarket.azure.com/dataset/bing/microsofttranslator) \nand enter them here." -> @ ;
ms_hc :horizontal -> @close="Close";

### Appearance settings tab

appearance :framed "Appearance" -> fontaa laf ;

# Font anti-aliasing settings

fontaa :double "Anti-aliasing settings" -> on | false | defaultaa | off | gasp | lcd_hrgb | lcd_hbgr | lcd_vrgb | lcd_vbgr | aa_help ;
on "Anti-aliasing on" -> "on " ;
false "Anti-aliasing false" -> "false " ;
defaultaa "Anti-aliasing default" -> "default " ;
off "Anti-aliasing off" -> "off " ;
gasp "Use font's built-in hinting instructions" -> "gasp " ;
lcd_hrgb "LCD monitor setting HRGB" -> "lcd_hrgb " ;
lcd_hbgr "LCD monitor setting HBGR" -> "lcd_hbgr " ;
lcd_vrgb "LCD monitor setting VRGB" -> "lcd_vrgb " ;
lcd_vbgr "LCD monitor setting VBGR" -> "lcd_vbgr " ;

aa_help :dialog "Help" -> aa_m aa_hc ;
aa_m :horizontal ->  aa_t ;
aa_t "Fonts in Java on Linux can be very unattractive unless the optimum settings are selected. \nThe optimum settings depend upon the font type and size, your hardware, and the anti-aliasing \nsettings. The best anti-aliasing settings may differ according to the selected font \nand size and are best found by trial and error. It is worth taking time to experiment \nwith these." -> @ ;
aa_hc :horizontal -> @close="Close";

# Look and feel settings

laf "Look and Feel" -> deflaf | gtk | motif | laf_help ;
deflaf "Default" -> " " ;
#gtk "GTK" -> " -Dswing.defaultlaf=com.sun.java.swing.plaf.gtk.GTKLookAndFeel " ;
gtk "GTK" -> " -Dswing.crossplatformlaf=com.sun.java.swing.plaf.gtk.GTKLookAndFeel ";
motif "Motif" -> " -Dswing.defaultlaf=com.sun.java.swing.plaf.motif.MotifLookAndFeel " ;
laf_help :dialog "Help" -> laf_m laf_hc ;
laf_m :horizontal ->  laf_t ;
laf_t "Modern Linux distributions attempt to create \na uniform look and feel, and you may notice little or no difference with these settings." -> @ ;
laf_hc :horizontal -> @close="Close";


### About tab

about :framed "About" -> general_t ;
general_t "About the Kaptain OmegaT Launcher" -> @text="Certain functions and settings of OmegaT are activated or configured at runtime, \ni.e. when OmegaT is launched, by means of command-line options. The Kaptain \nOmegaT Launcher is intended to make the use of these functions and settings easier \nfor Linux users by providing a user-friendly user interface. Information on Kaptain \nand its syntax can be found at: kaptain.sourceforge.net.\n\nThe Kaptain OmegaT Launcher is free software; you can redistribute it and/or modify it \nunder the terms of the GNU General Public License as published by the Free Software \nFoundation; either version 2 of the License, or (at your option) any later version.\n\n(c) Marc Prior, 2012\n\nThis program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; \nwithout even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. \nThe text of the GNU General Public License is provided within OmegaT itself, with which \nthis script is intended to be supplied." ;


### Button bar

button_bar :horizontal -> launch_button close_button ;
launch_button -> @exec(omt)="Launch" ;
close_button -> @close="Cancel" ;


### Launch command

omt -> javaloc " -jar " memory " -Duser.language=" UIlang " -Duser.country=" country " -Dgoogle.api.key=" gt " -Dmicrosoft.api.client_secret=" msk " -Dmicrosoft.api.client_id=" msi " -Dhttp.proxyHost=" proxyhost " -Dhttp.proxyPort=" proxyport " -Dawt.useSystemAAFontSettings=" fontaa laf " " jarfileloc " " projectloc " --config-dir=" configloc ;
