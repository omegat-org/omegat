plugins {
    id 'java-library'
}

dependencies {
    compileOnly(project.rootProject)
    if (providedLibsDir.directory) {
        compileOnly fileTree(dir: providedLibsDir, includes: ['**/commons-io-*.jar',
                                                              '**/slf4j-format-jdk14-*.jar'
                                                          ])
        implementation fileTree(dir: providedLibsDir, includes: ['**/jgit-*.jar'])
    } else {
        compileOnly(libs.slf4j.format.jdk14)
        implementation(libs.bundles.jgit)
        implementation(libs.svnkit)
    }
    testImplementation(testFixtures(project.rootProject))
}

jar {
    archiveFileName.set("teamtool.jar")
    destinationDirectory.set(rootProject.layout.buildDirectory.dir("modules").get())
    from(configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it)}) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    manifest {
        attributes('License': 'GNU Public License version 3 or later',
                'Implementation-Version': '6.1.0',
                'Main-Class': 'org.omegat.team.tool.TeamTool'
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
