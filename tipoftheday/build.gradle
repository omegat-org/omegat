/**
 * "Tip Of The Day" feature
 */

plugins {
    id 'java-library'
}

sourceSets {
    java {
        main {
            resources {
                srcDirs=['docs', 'src/main/resources']
            }
        }
    }
}

dependencies {
    compileOnly(project.rootProject)
    if (providedCoreLibsDir.directory) {
        compileOnly fileTree(dir: providedCoreLibsDir, includes: ['**/commons-lang3-*.jar', '**/jackson*.jar',
                                                                  '**/icu4j-*.jar'])
        implementation fileTree(dir: providedModuleLibsDir, includes: ['**/tipoftheday-*.jar'])
    } else {
        // runtime dependencies should be in main project
        implementation(libs.tipoftheday) {
            exclude module: 'commons-lang3'
            exclude module: 'commons-io'
        }
        compileOnly(libs.commons.lang3)
        runtimeOnly(libs.icu4j)

        // jackson-databind-yaml is dependency of languagetool-core
        compileOnly(libs.jackson.yaml)
    }
    testImplementation(testFixtures(project.rootProject))
    testImplementation(libs.commons.io)
    testRuntimeOnly(libs.icu4j)
}

jar {
    archiveAppendix.set("omegat")
    destinationDirectory.set(new File(rootProject.buildDir, "modules"))
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from configurations.runtimeClasspath.collect {it.isDirectory() ? it : zipTree(it)}
    def pluginProp = loadProperties(file('plugin.properties'))
    manifest {
        attributes('License': 'GNU Public License version 3 or later',
                'Implementation-Version': project.version,
                'OmegaT-Plugins': pluginProp.Class,
                'Plugin-Author': pluginProp.Author,
                'Plugin-Link': pluginProp.Link,
                'Plugin-Version': pluginProp.Version,
                'Plugin-Name': pluginProp.Name,
                'Plugin-Category': pluginProp.Category,
                'Plugin-Description': pluginProp.Description
        )
    }
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
}
